plugins {
    id 'com.jfrog.bintray' version '1.0'
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
    id 'com.github.johnrengelman.shadow' version '1.2.0'
    id "com.github.samueltbrown.cucumber" version "0.8"
}

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'codenarc'

version = '0.1.0'
group = 'com.github.lookout'
description = 'DeployDB is a tool to provide a single source of truth for artifact-based deployments'
// Needed for the application plugin's `run` task
mainClassName = 'deploydb.DeployDBApp'

////////////////////////////////////////////////////////////////////////////////
// DEPENDENCY MANAGEMENT
////////////////////////////////////////////////////////////////////////////////
configurations {
    localJavadocs
}

dependencies {
    compile withSources('org.codehaus.groovy:groovy-all:2.4.0+')

    [
     'dropwizard-core',
     'dropwizard-assets',
     'dropwizard-jersey',
     'dropwizard-hibernate',
     'dropwizard-views-mustache',
     'dropwizard-testing',
     ].each {
        compile withSources("io.dropwizard:${it}:0.8.0-rc1")
    }

    /* Used instead of the default HTTP connector for the Jersey client to
     * allow us to make PATCH requests
     */
    compile withSources('org.glassfish.jersey.connectors:jersey-apache-connector:2.+')

    /* Used for managing migrations */
    compile withSources('io.dropwizard.modules:dropwizard-flyway:0.7.0-1')

    /* Webhook support */
    compile 'com.github.lookout:whoas:0.1.2'

    compile withSources('com.h2database:h2:1.3.+')
    compile withSources('joda-time:joda-time:2.6+')

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.+'

    cucumberCompile 'info.cukes:cucumber-groovy:1.2.+'
}

repositories {
    jcenter()
    mavenCentral()

    maven { url 'http://dl.bintray.com/lookout/systems' }
}
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// TASKS
////////////////////////////////////////////////////////////////////////////////

cucumber {
    formats = ['pretty',
                // .toString() required here to make sure we convert a Groovy
                // string to a java.lang.String otherwise the underlying plugin
                // will explode
                "junit:${project.buildDir}/cucumber-results.xml".toString(),
                "html:docs/html5/restapi"]
    tags = ['~@wip']

    glueDirs = ['src/cucumber/groovy/step_definitions']
    featureDirs = ['features']
}
project.tasks.cucumber.doFirst {
    // NOTE: This hack exists because it appears that the cucumber gradle
    // plugin we're using does not properly create its own output dirs
    file("${buildDir}/resources/cucumber").mkdirs()
}
// The cucumber plugin makes the cucumber task depend on 'assemble' which is
// unnecessary and causes a cyclic task graph for the `shadowJar` task. We
// really just want to run our `cucumber` task under the `check` task and not
// require assembly
project.tasks.cucumber.dependsOn = project.tasks.cucumber.dependsOn.findAll {
    !((it instanceof ArrayList) && (it.get(0) == 'assemble'))
}
// Need a goofy way to refer to the task, see:
// https://github.com/samueltbrown/gradle-cucumber-plugin/issues/38
check.dependsOn project.tasks.cucumber


test {
    testLogging {
        /* we want more test failure information, see:
         *  <http://mrhaki.blogspot.com/2013/05/gradle-goodness-show-more-information.html>
         */
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

run {
    args 'server', 'deploydb.example.yml'
}

shadowJar {
    exclude 'META-INF/*.RSA', 'META-INF/*.DSA'
    manifest {
        attributes 'Main-Class' : mainClassName
    }

    // Only run after we've successfully completed our tests
    dependsOn check
}
assemble.dependsOn shadowJar
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// DOCUMENTATION TASKS
////////////////////////////////////////////////////////////////////////////////

asciidoctor {
    sourceDir 'src/asciidoc'
    outputDir 'docs'
    attributes 'toc': 'right',
                'source-highlighter': 'coderay',
                'toc-title': 'Table of Contents'
    shouldRunAfter test
}

groovydoc {
    destinationDir file('docs/html5/groovydoc')
    shouldRunAfter test
}

def withSources(String dependency) {
    ['sources'].each {
        def sourceDependency = dependencies.create("${dependency}:${it}")
        configurations.localJavadocs.dependencies.add(sourceDependency)
    }
    return dependency
}

task generateLocalJavadocCache(type: Javadoc) {
    destinationDir = file('generated-docs')

    source = configurations.localJavadocs.files.collect { sourceArtifact ->
        zipTree(sourceArtifact).findAll { it.name.endsWith('java') }
    }
    failOnError false
    title 'DeployDB Generated Docs'
}

////////////////////////////////////////////////////////////////////////////////
// PUBLISHING TASKS
////////////////////////////////////////////////////////////////////////////////

task publishDocs(type: Exec) {
    /* If for whatever reason you need to force-push the gh-pages branch this
     * command will work:
     *    % git push origin `git subtree split --prefix docs/html5 master`:gh-pages --force
     */

    group 'Publishing'
    description 'Publish the Asciidoctor docs to gh-pages'
    commandLine 'git'
    args 'subtree', 'push', '--prefix', 'docs/html5', 'upstream', 'gh-pages'
    dependsOn asciidoctor
}


bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'lookout'
        repo = 'systems'
        name = 'DeployDB'
        labels = []

        version {
            name = project.version
            vcsTag = "v${project.version}"
            desc = project.description
        }
    }
}
bintrayUpload.dependsOn assemble
////////////////////////////////////////////////////////////////////////////////
// vim: ft=groovy
